use std::str::FromStr;
use crate::ast::{Expr, Operator};

grammar;

ExprOp: Operator = {
    "&&" => Operator::And,
    "||" => Operator::Or
};

LogicOp: Operator = {
    "<" => Operator::LessThan,
    ">" => Operator::GreaterThan,
    "!=" => Operator::NotEq,
    "==" => Operator::Eq,
};

MathOp: Operator = {
    "+" => Operator::Add,
    "-" => Operator::Sub,
};

FactorOp: Operator = {
    "*" => Operator::Mul,
    "/" => Operator::Div,
};

PipeOp: Operator = {
    "|>" => Operator::Pipe,
};

pub Expr: Box<Expr> = {
    Expr ExprOp LogicExpr => Box::new(Expr::Op(<>)),
    LogicExpr,
};

LogicExpr: Box<Expr> = {
    MathExpr LogicOp MathExpr => Box::new(Expr::Op(<>)),
    MathExpr,
};

MathExpr: Box<Expr> = {
    MathExpr MathOp Term => Box::new(Expr::Op(<>)),
    Term,
};

Term: Box<Expr> = {
    Term FactorOp Factor => Box::new(Expr::Op(<>)),
    Factor,
};

Factor: Box<Expr> = {
    Factor PipeOp PipeItem => Box::new(Expr::Op(<>)),
    Item,
};

PipeItem: Box<Expr> = {
    "forward" <Id> => Box::new(Expr::Id(<>)),
    Id => Box::new(Expr::Id(<>)),
};

Item: Box<Expr> = {
    Num => Box::new(Expr::Number(<>)),
    Id => Box::new(Expr::Id(<>)),
    "(" <Expr> ")",
};


Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};

Id: String = {
    r"[_a-zA-z]+[0-9_a-zA-z]*" => String::from(<>)
};

