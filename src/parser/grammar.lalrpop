use std::str::FromStr;
use crate::ast::{Expr, Operator, TypeConst};

grammar;

// Types

Int: TypeConst = NUMBER => TypeConst::Int(i32::from_str(<>).unwrap());

Float: TypeConst = <f: r"[0-9]+\.[0-9]+"> => TypeConst::Float(f.parse::<f32>().unwrap());

Bool: TypeConst = {
    "true" => TypeConst::Bool(true),
    "false" => TypeConst::Bool(false)
};

Str: TypeConst = {
    <s: r#""[^"]*""#> => TypeConst::String(String::from(&s[1..s.len() - 1]))
};

Vector: TypeConst = {
    "[" <mut vec: (<Expr> ",")*> <c: Expr?> "]" => match c {
        None => TypeConst::Vector(vec),
        Some(c) => {
            vec.push(c);
            TypeConst::Vector(vec)
        }
    }
}

SimpleConst = {
    Int,
    Str,
    Float,
    Bool
};

Const = {
    SimpleConst,
    Vector
}

Id: Box<Expr> = {
    ID => Box::new(Expr::Id(String::from(<>)))
};

// Operators

ExprOp: Operator = {
    "&&" => Operator::And,
    "||" => Operator::Or
};

LogicOp: Operator = {
    "<" => Operator::LessThan,
    ">" => Operator::GreaterThan,
    "!=" => Operator::NotEq,
    "==" => Operator::Eq,
};

MathOp: Operator = {
    "+" => Operator::Add,
    "-" => Operator::Sub,
};

FactorOp: Operator = {
    "*" => Operator::Mul,
    "/" => Operator::Div,
};

PipeOp: Operator = {
    "|>" => Operator::Pipe,
};

// Expressions

Tier<Operator, NextTerm>: Box<Expr> = {
    Tier<Operator, NextTerm> Operator NextTerm => Box::new(Expr::Op(<>)),
    NextTerm
}

pub Expr: Box<Expr> = Tier<ExprOp, LogicExpr>;

LogicExpr: Box<Expr> = {
    MathExpr LogicOp MathExpr => Box::new(Expr::Op(<>)),
    MathExpr,
};

MathExpr: Box<Expr> = Tier<MathOp, Term>;

Term: Box<Expr> = Tier<FactorOp, Factor>;

Factor: Box<Expr> = {
    <factor:Factor> PipeOp "forward" <id:Id> => Box::new(Expr::Op(factor, Operator::ForwardPipe, id)),
    Factor PipeOp Id => Box::new(Expr::Op(<>)),
    Item,
};

Item: Box<Expr> = {
    Id,
    Const => Box::new(Expr::Const(<>)),
    "(" <Expr> ")" => Box::new(Expr::ParenthOp(<>)),
};


match {
    r"[0-9]+" => NUMBER,
} else {
    r"[a-z_A-Z]+[0-9_a-zA-Z]*" => ID,
    _
}

