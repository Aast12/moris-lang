use std::str::FromStr;
use crate::ast::{Dimension, types::Variable, FunctionParam, types::FunctionSignature};
use crate::ast;
use crate::ast::types;
use crate::ast::expressions::{Expression, Index};
use crate::ast::expressions::constant::TypeConst;
use crate::ast::expressions::constant;
use crate::ast::types::DataType;
use crate::ast::expressions::operation;
use crate::ast::expressions::id::{Access, Id};
use crate::ast::expressions::call;


grammar<'m>;


// Macros

PComma<TItem>: Vec<TItem> = {
    <mut vec: (<TItem> COMMA)*> <c: TItem?> => match c {
        None => vec,
        Some(c) => {
            vec.push(c);
            vec
        }
    }
}

PBoxed<T>: Box<T> = T => Box::new(<>);

// Types

PType: DataType = {
    T_INT => DataType::Int,
    T_FLOAT => DataType::Float,
    T_BOOl => DataType::Bool,
    T_STR => DataType::String,
    T_DATAFRAME => DataType::DataFrame,
}

PFunctionType: DataType = {
    PType,
    T_VOID => DataType::Void
}

PInt: constant::Const<'m> = NUMBER => constant::Const::new(<>, DataType::Int);

PFloat: constant::Const<'m> = <f: FLOAT> => constant::Const::new(<>, DataType::Float);

PBool: constant::Const<'m> = {
    TRUE => constant::Const::new(<>, DataType::Bool),
    FALSE => constant::Const::new(<>, DataType::Bool)
}

PStr: constant::Const<'m> = <s: STRING> => constant::Const::new(&s[1..s.len() - 1], DataType::String);

// PVectorConst: TypeConst = LBRACKET <PComma<Expr>> RBRACKET => TypeConst::Vector(<>);

PSimpleConst = {
    PInt,
    PStr,
    PFloat,
    PBool
}

PConst = {
    PSimpleConst,
    //PVectorConst
}

PId: Expression<'m> = ID => 
    Expression::Access(Access::new(
        Id::new(<>, None),
        vec![],
    ));

// Variables

PIndexing: Index<'m> = {
    <begin:Expr> COLON <end:Expr> => Index::Range(begin, end),
    <idx:Expr> => Index::Simple(idx)
}

PDimension: Dimension<'m> = {
    LBRACKET <a: Expr> RLBRACKET <b: Expr> RBRACKET => Dimension(2, vec![a, b]),
    LBRACKET <Expr> RBRACKET => Dimension(1, vec![<>])
}

PVarDeclaration: types::Variable<'m> = {
    LET <id: ID> COLON <data_type: PType> <dim: PDimension?> <value: (ASSIGN <Expr>)?> SEMICOLON => types::Variable {
        id: String::from(id),
        data_type,
        dimension: dim.unwrap_or(Dimension(0, vec![])),
        value
    }
}

// TODO Fix w custom tokenizer, allow multiple dimensions and raise error on higher than two
PVarRefIndex: Vec<Index<'m>> = {
    LBRACKET <PIndexing> RBRACKET  => vec![<>],
    LBRACKET <a:PIndexing> RLBRACKET <b:PIndexing> RBRACKET => vec![<>]
}

PVarReference: Access<'m> = {
    <id: ID> <indexing: PVarRefIndex?> => 
        Access::new(
            Id::new(id, None),
            indexing.unwrap_or(vec![])
        )
}

PVarAssignment: ast::Statement<'m> = <var: PVarReference> ASSIGN <exp: Expr> => ast::Statement::VarAssign(var, exp);

// Operators

PExprOp: types::Operator = {
    AND => types::Operator::And,
    OR => types::Operator::Or
}

PLogicOp: types::Operator = {
    LT => types::Operator::LessThan,
    GT => types::Operator::GreaterThan,
    NOTEQUAL => types::Operator::NotEq,
    EQUAL => types::Operator::Eq,
}

PMathOp: types::Operator = {
    ADD => types::Operator::Add,
    SUB => types::Operator::Sub,
}

PFactorOp: types::Operator = {
    MUL => types::Operator::Mul,
    DIV => types::Operator::Div,
}

PPipeOp: types::Operator = PIPE => types::Operator::Pipe;

// Expressions

PTier<Op, NextTerm>: Box<Expression<'m>> = {
    PTier<Op, NextTerm> Op NextTerm => 
        Box::new(
            Expression::Op(
                operation::Operation::new(<>)
            )
        ),
    NextTerm
}

PExpr: Box<Expression<'m>> = PTier<PExprOp, PLogicExpr>;

PLogicExpr: Box<Expression<'m>> = {
    PMathExpr PLogicOp PMathExpr => 
        Box::new(
            Expression::Op(
                operation::Operation::new(<>)
            )
        ),
    PMathExpr,
}

PMathExpr: Box<Expression<'m>> = PTier<PMathOp, PTerm>;

PTerm: Box<Expression<'m>> = PTier<PFactorOp, PFactor>;

PFactor: Box<Expression<'m>> = {
    <factor:PFactor> PPipeOp FORWARD <id:PBoxed<PId>> => 
        Box::new(
            Expression::Op(
                operation::Operation::new(factor, types::Operator::ForwardPipe, id)
            )
        ),
    PFactor PPipeOp PBoxed<PId> => 
        Box::new(
            Expression::Op(
                operation::Operation::new(<>)
            )
        ),
    PItem,
}

PItem: Box<Expression<'m>> = {
    PVarReference => Box::new(Expression::Access(<>)),
    PConst => Box::new(Expression::Const(<>)),
    LPAREN <PExpr> RPAREN => <>,
    PFunctionCall => Box::new(Expression::Call(<>))
}

// Functions


PFuncParam: FunctionParam = <id: ID> COLON <dtype: PType> => FunctionParam(String::from(id), dtype);

PCallParams = PComma<PExpr>;

PFunctionParams = PComma<PFuncParam>;

PFnSignature: types::FunctionSignature = FN <id: CALL_ID> <params: PFunctionParams> RPAREN COLON <dtype: PType> => types::FunctionSignature {
    id:  String::from(&id[..id.len() - 1]),
    data_type: dtype,
    params: params
};

PFunctionItem: ast::Statement<'m> = {
    PStatement,
    PVarDeclaration => ast::Statement::VarDeclaration(<>),
}

PFunctionBlock: ast::Block<'m> = LCBRACKET <statements: PFunctionItem*> RCBRACKET => ast::Block(statements);

PFunction: ast::Function<'m> = <signature: PFnSignature> <block: PFunctionBlock> => ast::Function {
    signature: signature,
    block: block
};

PFunctionCall: call::Call<'m> = <id: CALL_ID> <params: PCallParams> RPAREN => 
        call::Call::new(&id[..id.len() - 1], params);

PReturnStatement: ast::Statement<'m> = RETURN <PExpr> SEMICOLON => ast::Statement::Return(<>);

PStatement = {
    PCondition,
    PLoop,
    PReturnStatement,
    PLineStatement,
}

PGlobalStatement: ast::Statement<'m> = {
    PFunction => ast::Statement::FunctionDeclaration(<>),
    PStatement,
    PVarDeclaration =>  ast::Statement::VarDeclaration(<>)
}

PBlock: ast::Block<'m> = {
    PStatement => ast::Block(vec![<>]),
    PStrictBlock
}

PLineStatement: ast::Statement<'m> = {
    <PVarAssignment> SEMICOLON,
    <PExpr> SEMICOLON => ast::Statement::Expression(<>)
}

PElseBlock: ast::Block<'m> = {
    PCondition => ast::Block(vec![<>]),
    PStrictBlock
}

PCondition: ast::Statement<'m> = {
    IF <condition: PExpr> RPAREN <if_block: PStrictBlock> <else_block: (ELSE <PElseBlock>)?> => 
        ast::Statement::If {
            condition: condition,
            if_block: if_block,
            else_block: else_block
        }
}

PStrictBlock: ast::Block<'m> = LCBRACKET <statements: PStatement*> RCBRACKET => ast::Block(statements);

PLoop = {
    PForLoop,
    PWhileLoop
}

PForLoop: ast::Statement<'m> = FOR <id: ID> IN <iterable: PExpr> RPAREN <block: PBlock> =>
    ast::Statement::For {
        iterator_id: String::from(id),
        iterable: iterable,
        block: block
    };

PWhileLoop: ast::Statement<'m> = WHILE <condition: PExpr> RPAREN <block: PBlock> =>
    ast::Statement::While {
        condition: condition,
        block: block
    };

pub PProgram: ast::Program<'m> = PGlobalStatement* => ast::Program(<>);

match {
    r#""[^"]*""# => STRING,
    "&&" => AND,
    "||" => OR,
    "<" => LT,
    ">" => GT,
    "!=" => NOTEQUAL,
    "==" => EQUAL,
    "+" => ADD,
    "-" => SUB,
    "*" => MUL,
    "/" => DIV,
    "|>" => PIPE,
    "forward" => FORWARD,
    "int" => T_INT,
    "float" => T_FLOAT,
    "bool" => T_BOOl,
    "str" => T_STR,
    "DataFrame" => T_DATAFRAME,
    "void" => T_VOID,
    "let" => LET,
    "true" => TRUE,
    "false" => FALSE,
    "fn" => FN,
    "return" => RETURN,
    "for",
    r"for\s*\(" => FOR, // TODO Fix joint ( w/ custom tokenizer,
    "while",
    r"while\s*\(" => WHILE,
    "in" => IN,
    "(" => LPAREN,
    ")" => RPAREN,
    "if",
    r"if\s*\(" => IF,
    "else" => ELSE,
    "{" => LCBRACKET,
    "}" => RCBRACKET,
    "[" => LBRACKET,
    "]" => RBRACKET,
    "][" => RLBRACKET,
    "=" => ASSIGN,
    "," => COMMA,
    ":" => COLON,
    ";" => SEMICOLON,
    r"[a-z_A-Z]+[0-9_a-zA-Z]*" => ID,
    r"[0-9]+\.[0-9]+" => FLOAT,
    r"[0-9]+" => NUMBER,
} else {
    r"[a-z_A-Z]+[0-9_a-zA-Z]*\(" => CALL_ID,
    _
}

