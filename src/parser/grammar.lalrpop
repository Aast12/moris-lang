use std::str::FromStr;
use crate::ast::{Dimension};


use crate::ast::types::{DataType, Function, FunctionParam, Operator, Variable, FunctionSignature};

use crate::ast::expressions::{Expression, Index};


use crate::ast::expressions::constant::{Const, TypeConst};

use crate::ast::expressions::id::{Access, Id};
use crate::ast::expressions::operation::Operation;
use crate::ast::expressions::call::Call;
use crate::ast::statements::{Block, Program, Statement};


grammar<'m>;


// Macros

PComma<TItem>: Vec<TItem> = {
    <mut vec: (<TItem> COMMA)*> <c: TItem?> => match c {
        None => vec,
        Some(c) => {
            vec.push(c);
            vec
        }
    }
}

PBoxed<T>: Box<T> = T => Box::new(<>);

// Types

PType: DataType = {
    T_INT => DataType::Int,
    T_FLOAT => DataType::Float,
    T_BOOl => DataType::Bool,
    T_STR => DataType::String,
    T_DATAFRAME => DataType::DataFrame,
}

PFunctionType: DataType = {
    PType,
    T_VOID => DataType::Void
}

PInt: Const<'m> = NUMBER => Const::new(<>, DataType::Int);

PFloat: Const<'m> = <f: FLOAT> => Const::new(<>, DataType::Float);

PBool: Const<'m> = {
    TRUE => Const::new(<>, DataType::Bool),
    FALSE => Const::new(<>, DataType::Bool)
}

PStr: Const<'m> = <s: STRING> => Const::new(&s[1..s.len() - 1], DataType::String);

// PVectorConst: TypeConst = LBRACKET <PComma<PExpr>> RBRACKET => TypeConst::Vector(<>);

PSimpleConst = {
    PInt,
    PStr,
    PFloat,
    PBool
}

PConst = {
    PSimpleConst,
    //PVectorConst
}

PId: Expression<'m> = ID => 
    Expression::Access(Access::new(
        Id::new(<>, None),
        vec![],
    ));

// Variables

PIndexing: Index<'m> = {
    <begin:PExpr> COLON <end:PExpr> => Index::Range(begin, end),
    <idx:PExpr> => Index::Simple(idx)
}

PDimension: Dimension<'m> = {
    LBRACKET <a: PExpr> RLBRACKET <b: PExpr> RBRACKET => Dimension(2, vec![a, b]),
    LBRACKET <PExpr> RBRACKET => Dimension(1, vec![<>])
}

PVarDeclaration: Variable<'m> = {
    LET <id: ID> COLON <data_type: PType> <dim: PDimension?> <value: (ASSIGN <PExpr>)?> SEMICOLON => Variable::new(
        String::from(id),
        data_type,
        dim.unwrap_or(Dimension(0, vec![])),
        value
    )
}

// TODO Fix w custom tokenizer, allow multiple dimensions and raise error on higher than two
PVarRefIndex: Vec<Index<'m>> = {
    LBRACKET <PIndexing> RBRACKET  => vec![<>],
    LBRACKET <a:PIndexing> RLBRACKET <b:PIndexing> RBRACKET => vec![<>]
}

PVarReference: Access<'m> = {
    <id: ID> <indexing: PVarRefIndex?> => 
        Access::new(
            Id::new(id, None),
            indexing.unwrap_or(vec![])
        )
}

PVarAssignment: Statement<'m> = <var: PVarReference> ASSIGN <exp: PExpr> => Statement::VarAssign(var, exp);

// Operators

PExprOp: Operator = {
    AND => Operator::And,
    OR => Operator::Or
}

PLogicOp: Operator = {
    LT => Operator::LessThan,
    GT => Operator::GreaterThan,
    NOTEQUAL => Operator::NotEq,
    EQUAL => Operator::Eq,
}

PMathOp: Operator = {
    ADD => Operator::Add,
    SUB => Operator::Sub,
}

PFactorOp: Operator = {
    MUL => Operator::Mul,
    DIV => Operator::Div,
}

PPipeOp: Operator = PIPE => Operator::Pipe;

// Expressions

PTier<Op, NextTerm>: Box<Expression<'m>> = {
    PTier<Op, NextTerm> Op NextTerm => 
        Box::new(
            Expression::Op(
                Operation::new(<>)
            )
        ),
    NextTerm
}

PExpr: Box<Expression<'m>> = PTier<PExprOp, PLogicExpr>;

PLogicExpr: Box<Expression<'m>> = {
    PMathExpr PLogicOp PMathExpr => 
        Box::new(
            Expression::Op(
                Operation::new(<>)
            )
        ),
    PMathExpr,
}

PMathExpr: Box<Expression<'m>> = PTier<PMathOp, PTerm>;

PTerm: Box<Expression<'m>> = PTier<PFactorOp, PFactor>;

PFactor: Box<Expression<'m>> = {
    <factor:PFactor> PPipeOp FORWARD <id:PBoxed<PId>> => 
        Box::new(
            Expression::Op(
                Operation::new(factor, Operator::ForwardPipe, id)
            )
        ),
    PFactor PPipeOp PBoxed<PId> => 
        Box::new(
            Expression::Op(
                Operation::new(<>)
            )
        ),
    PItem,
}

PItem: Box<Expression<'m>> = {
    PVarReference => Box::new(Expression::Access(<>)),
    PConst => Box::new(Expression::Const(<>)),
    LPAREN <PExpr> RPAREN => <>,
    PFunctionCall => Box::new(Expression::Call(<>))
}

// Functions


PFuncParam: FunctionParam = <id: ID> COLON <dtype: PType> => FunctionParam(String::from(id), dtype);

PCallParams = PComma<PExpr>;

PFunctionParams = PComma<PFuncParam>;

PFnSignature: FunctionSignature = FN <id: CALL_ID> <params: PFunctionParams> RPAREN COLON <dtype: PType> => FunctionSignature {
    id:  String::from(&id[..id.len() - 1]),
    data_type: dtype,
    params: params
};

PFunctionItem: Statement<'m> = {
    PStatement,
    PVarDeclaration => Statement::VarDeclaration(<>),
}

PFunctionBlock: Block<'m> = LCBRACKET <statements: PFunctionItem*> RCBRACKET => Block(statements);

PFunction: Function<'m> = <signature: PFnSignature> <block: PFunctionBlock> => Function::new(<>);

PFunctionCall: Call<'m> = <id: CALL_ID> <params: PCallParams> RPAREN => 
        Call::new(&id[..id.len() - 1], params);

PReturnStatement: Statement<'m> = RETURN <PExpr> SEMICOLON => Statement::Return(<>);

PStatement = {
    PCondition,
    PLoop,
    PReturnStatement,
    PLineStatement,
}

PGlobalStatement: Statement<'m> = {
    PFunction => Statement::FunctionDeclaration(<>),
    PStatement,
    PVarDeclaration =>  Statement::VarDeclaration(<>)
}

PBlock: Block<'m> = {
    PStatement => Block(vec![<>]),
    PStrictBlock
}

PLineStatement: Statement<'m> = {
    <PVarAssignment> SEMICOLON,
    <PExpr> SEMICOLON => Statement::Expression(<>)
}

PElseBlock: Block<'m> = {
    PCondition => Block(vec![<>]),
    PStrictBlock
}

PCondition: Statement<'m> = {
    IF <condition: PExpr> RPAREN <if_block: PStrictBlock> <else_block: (ELSE <PElseBlock>)?> => 
        Statement::If {
            condition: condition,
            if_block: if_block,
            else_block: else_block
        }
}

PStrictBlock: Block<'m> = LCBRACKET <statements: PStatement*> RCBRACKET => Block(statements);

PLoop = {
    PForLoop,
    PWhileLoop
}

PForLoop: Statement<'m> = FOR <id: ID> IN <iterable: PExpr> RPAREN <block: PBlock> =>
    Statement::For {
        iterator_id: String::from(id),
        iterable: iterable,
        block: block
    };

PWhileLoop: Statement<'m> = WHILE <condition: PExpr> RPAREN <block: PBlock> =>
    Statement::While {
        condition: condition,
        block: block
    };

pub PProgram: Program<'m> = PGlobalStatement* => Program(<>);

match {
    r#""[^"]*""# => STRING,
    "&&" => AND,
    "||" => OR,
    "<" => LT,
    ">" => GT,
    "!=" => NOTEQUAL,
    "==" => EQUAL,
    "+" => ADD,
    "-" => SUB,
    "*" => MUL,
    "/" => DIV,
    "|>" => PIPE,
    "forward" => FORWARD,
    "int" => T_INT,
    "float" => T_FLOAT,
    "bool" => T_BOOl,
    "str" => T_STR,
    "DataFrame" => T_DATAFRAME,
    "void" => T_VOID,
    "let" => LET,
    "true" => TRUE,
    "false" => FALSE,
    "fn" => FN,
    "return" => RETURN,
    "for",
    r"for\s*\(" => FOR, // TODO Fix joint ( w/ custom tokenizer,
    "while",
    r"while\s*\(" => WHILE,
    "in" => IN,
    "(" => LPAREN,
    ")" => RPAREN,
    "if",
    r"if\s*\(" => IF,
    "else" => ELSE,
    "{" => LCBRACKET,
    "}" => RCBRACKET,
    "[" => LBRACKET,
    "]" => RBRACKET,
    "][" => RLBRACKET,
    "=" => ASSIGN,
    "," => COMMA,
    ":" => COLON,
    ";" => SEMICOLON,
    r"[a-z_A-Z]+[0-9_a-zA-Z]*" => ID,
    r"[0-9]+\.[0-9]+" => FLOAT,
    r"[0-9]+" => NUMBER,
} else {
    r"[a-z_A-Z]+[0-9_a-zA-Z]*\(" => CALL_ID,
    _
}

