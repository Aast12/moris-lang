use std::str::FromStr;
use crate::ast::{Expr, TypeConst, Index, Dimension, Variable, VarRef, FunctionParam, FunctionSignature};
use crate::ast;
use crate::ast::types;

grammar;


// Macros

Comma<TItem>: Vec<TItem> = {
    <mut vec: (<TItem> COMMA)*> <c: TItem?> => match c {
        None => vec,
        Some(c) => {
            vec.push(c);
            vec
        }
    }
}

Boxed<T>: Box<T> = T => Box::new(<>);

// Types

Type: types::DataType = {
    T_INT => types::DataType::Int,
    T_FLOAT => types::DataType::Float,
    T_BOOl => types::DataType::Bool,
    T_STR => types::DataType::String,
    T_DATAFRAME => types::DataType::DataFrame,
}

FunctionType: types::DataType = {
    Type,
    T_VOID => types::DataType::Void
}

Int: TypeConst = NUMBER => TypeConst::Int(i32::from_str(<>).unwrap());

Float: TypeConst = <f: FLOAT> => TypeConst::Float(f.parse::<f32>().unwrap());

Bool: TypeConst = {
    TRUE => TypeConst::Bool(true),
    FALSE => TypeConst::Bool(false)
}

Str: TypeConst = {
    <s: STRING> => TypeConst::String(String::from(&s[1..s.len() - 1]))
}

VectorConst: TypeConst = LBRACKET <Comma<Expr>> RBRACKET => TypeConst::Vector(<>);

SimpleConst = {
    Int,
    Str,
    Float,
    Bool
}

Const = {
    SimpleConst,
    VectorConst
}

Id: Expr = ID => Expr::Var(VarRef {
    id: String::from(<>),
    indexing: None
});

// Variables

Indexing: Index = {
    <begin:Expr> COLON <end:Expr> => Index::Range(begin, end),
    <idx:Expr> => Index::Simple(idx)
}

Dimension: Dimension = {
    LBRACKET <a: Expr> RLBRACKET <b: Expr> RBRACKET => Dimension(2, vec![a, b]),
    LBRACKET <Expr> RBRACKET => Dimension(1, vec![<>])
}

VarDeclaration: Variable = {
    LET <id: ID> COLON <dtype: Type> <dim: Dimension?> <value: (ASSIGN <Expr>)?> SEMICOLON => Variable {
        id: String::from(id),
        data_type: dtype,
        dimension: dim.unwrap_or(Dimension(0, vec![])),
        value: value
    }
}

// TODO Fix w custom tokenizer, allow multiple dimensions and raise error on higher than two
VarRefIndex: Vec<Index> = {
    LBRACKET <Indexing> RBRACKET  => vec![<>],
    LBRACKET <a:Indexing> RLBRACKET <b:Indexing> RBRACKET => vec![<>]
}

VarReference: VarRef = {
    <id: ID> <indexing: VarRefIndex?> => VarRef {
        id: String::from(id),
        indexing: indexing
    } 
}

VarAssignment: ast::Statement = <var: VarReference> ASSIGN <exp: Expr> => ast::Statement::VarAssign(var, exp);


// Operators

ExprOp: types::Operator = {
    AND => types::Operator::And,
    OR => types::Operator::Or
}

LogicOp: types::Operator = {
    LT => types::Operator::LessThan,
    GT => types::Operator::GreaterThan,
    NOTEQUAL => types::Operator::NotEq,
    EQUAL => types::Operator::Eq,
}

MathOp: types::Operator = {
    ADD => types::Operator::Add,
    SUB => types::Operator::Sub,
}

FactorOp: types::Operator = {
    MUL => types::Operator::Mul,
    DIV => types::Operator::Div,
}

PipeOp: types::Operator = PIPE => types::Operator::Pipe;

// Expressions

Tier<Op, NextTerm>: Box<Expr> = {
    Tier<Op, NextTerm> Op NextTerm => Box::new(Expr::Op(<>)),
    NextTerm
}

Expr: Box<Expr> = Tier<ExprOp, LogicExpr>;

LogicExpr: Box<Expr> = {
    MathExpr LogicOp MathExpr => Box::new(Expr::Op(<>)),
    MathExpr,
}

MathExpr: Box<Expr> = Tier<MathOp, Term>;

Term: Box<Expr> = Tier<FactorOp, Factor>;

Factor: Box<Expr> = {
    <factor:Factor> PipeOp FORWARD <id:Boxed<Id>> => Box::new(Expr::Op(factor, types::Operator::ForwardPipe, id)),
    Factor PipeOp Boxed<Id> => Box::new(Expr::Op(<>)),
    Item,
}

Item: Box<Expr> = {
    VarReference => Box::new(Expr::Var(<>)),
    Const => Box::new(Expr::Const(<>)),
    LPAREN <Expr> RPAREN => Box::new(Expr::ParenthOp(<>)),
    FunctionCall => Box::new(<>)
}

// Functions


FuncParam: FunctionParam = <id: ID> COLON <dtype: Type> => FunctionParam(String::from(id), dtype);

CallParams = Comma<Expr>;

FunctionParams = Comma<FuncParam>;

FnSignature: FunctionSignature = FN <id: CALL_ID> <params: FunctionParams> RPAREN COLON <dtype: Type> => FunctionSignature {
    id:  String::from(&id[..id.len() - 1]),
    data_type: dtype,
    params: params
};

FunctionItem: ast::Statement = {
    Statement,
    VarDeclaration => ast::Statement::VarDeclaration(<>),
}

FunctionBlock: ast::Block = LCBRACKET <statements: FunctionItem*> RCBRACKET => ast::Block(statements);

Function: ast::Function = <signature: FnSignature> <block: FunctionBlock> => ast::Function {
    signature: signature,
    block: block
};

FunctionCall: Expr = <id: CALL_ID> <params: CallParams> RPAREN => 
    Expr::FunctionCall(String::from(&id[..id.len() - 1]), params);

ReturnStatement: ast::Statement = RETURN <Expr> SEMICOLON => ast::Statement::Return(<>);

Statement = {
    Condition,
    Loop,
    ReturnStatement,
    LineStatement,
}

GlobalStatement: ast::Statement = {
    Function => ast::Statement::FunctionDeclaration(<>),
    Statement,
    VarDeclaration =>  ast::Statement::VarDeclaration(<>)
}

Block: ast::Block = {
    Statement => ast::Block(vec![<>]),
    StrictBlock
}

LineStatement: ast::Statement = {
    <VarAssignment> SEMICOLON,
    <Expr> SEMICOLON => ast::Statement::Expression(<>)
}

ElseBlock: ast::Block = {
    Condition => ast::Block(vec![<>]),
    StrictBlock
}

Condition: ast::Statement = {
    IF <condition: Expr> RPAREN <if_block: StrictBlock> <else_block: (ELSE <ElseBlock>)?> => 
        ast::Statement::If {
            condition: condition,
            if_block: if_block,
            else_block: else_block
        }
}

StrictBlock: ast::Block = LCBRACKET <statements: Statement*> RCBRACKET => ast::Block(statements);

Loop = {
    ForLoop,
    WhileLoop
}

ForLoop: ast::Statement = FOR <id: ID> IN <iterable: Expr> RPAREN <block: Block> =>
    ast::Statement::For {
        iterator_id: String::from(id),
        iterable: iterable,
        block: block
    };

WhileLoop: ast::Statement = WHILE <condition: Expr> RPAREN <block: Block> =>
    ast::Statement::While {
        condition: condition,
        block: block
    };

pub Program: ast::Program = GlobalStatement* => ast::Program(<>);

match {
    r#""[^"]*""# => STRING,
    "&&" => AND,
    "||" => OR,
    "<" => LT,
    ">" => GT,
    "!=" => NOTEQUAL,
    "==" => EQUAL,
    "+" => ADD,
    "-" => SUB,
    "*" => MUL,
    "/" => DIV,
    "|>" => PIPE,
    "forward" => FORWARD,
    "int" => T_INT,
    "float" => T_FLOAT,
    "bool" => T_BOOl,
    "str" => T_STR,
    "DataFrame" => T_DATAFRAME,
    "void" => T_VOID,
    "let" => LET,
    "true" => TRUE,
    "false" => FALSE,
    "fn" => FN,
    "return" => RETURN,
    "for",
    r"for\s*\(" => FOR, // TODO Fix joint ( w/ custom tokenizer,
    "while",
    r"while\s*\(" => WHILE,
    "in" => IN,
    "(" => LPAREN,
    ")" => RPAREN,
    "if",
    r"if\s*\(" => IF,
    "else" => ELSE,
    "{" => LCBRACKET,
    "}" => RCBRACKET,
    "[" => LBRACKET,
    "]" => RBRACKET,
    "][" => RLBRACKET,
    "=" => ASSIGN,
    "," => COMMA,
    ":" => COLON,
    ";" => SEMICOLON,
    r"[a-z_A-Z]+[0-9_a-zA-Z]*" => ID,
    r"[0-9]+\.[0-9]+" => FLOAT,
    r"[0-9]+" => NUMBER,
} else {
    r"[a-z_A-Z]+[0-9_a-zA-Z]*\(" => CALL_ID,
    _
}

