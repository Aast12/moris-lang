use std::str::FromStr;
use crate::ast::{Expr, Operator, TypeConst, DataType, Index, Dimension, Variable, VarRef, VarAssign};

grammar;


// Macros

Comma<TItem>: Vec<TItem> = {
    <mut vec: (<TItem> COMMA)*> <c: TItem?> => match c {
        None => vec,
        Some(c) => {
            vec.push(c);
            vec
        }
    }
}

Boxed<T>: Box<T> = T => Box::new(<>);

// Types

Type: DataType = {
    T_INT => DataType::Int,
    T_FLOAT => DataType::Float,
    T_BOOl => DataType::Bool,
    T_STR => DataType::String,
    T_DATAFRAME => DataType::DataFrame,
}

FunctionType: DataType = {
    Type,
    T_VOID => DataType::Void
}

Int: TypeConst = NUMBER => TypeConst::Int(i32::from_str(<>).unwrap());

Float: TypeConst = <f: FLOAT> => TypeConst::Float(f.parse::<f32>().unwrap());

Bool: TypeConst = {
    TRUE => TypeConst::Bool(true),
    FALSE => TypeConst::Bool(false)
}

Str: TypeConst = {
    <s: STRING> => TypeConst::String(String::from(&s[1..s.len() - 1]))
}

VectorConst: TypeConst = LBRACKET <Comma<Expr>> RBRACKET => TypeConst::Vector(<>);

SimpleConst = {
    Int,
    Str,
    Float,
    Bool
}

Const = {
    SimpleConst,
    VectorConst
}

Id: Expr = ID => Expr::Var(VarRef {
    id: String::from(<>),
    indexing: None
});

// Variables

pub Indexing: Index = {
    <begin:Expr> COLON <end:Expr> => Index::Range(begin, end),
    <idx:Expr> => Index::Simple(idx)
}

pub Dimension: Dimension = {
    LBRACKET <a: Expr> RLBRACKET <b: Expr> RBRACKET => Dimension(2, vec![a, b]),
    LBRACKET <Expr> RBRACKET => Dimension(1, vec![<>])
}

pub VarDeclaration: Variable = {
    LET <id: ID> COLON <dtype: Type> <dim: Dimension?> <value: (ASSIGN <Expr>)?> => Variable {
        id: String::from(id),
        data_type: dtype,
        dimension: dim.unwrap_or(Dimension(0, vec![])),
        value: value
    }
}

// TODO Fix w custom tokenizer, allow multiple dimensions and raise error on higher than two
VarRefIndex: Vec<Index> = {
    LBRACKET <Indexing> RBRACKET  => vec![<>],
    LBRACKET <a:Indexing> RLBRACKET <b:Indexing> RBRACKET => vec![<>]
}

pub VarReference: VarRef = {
    <id: ID> <indexing: VarRefIndex?> => VarRef {
        id: String::from(id),
        indexing: indexing
    } 
}

pub VarAssignment: VarAssign = <var: VarReference> ASSIGN <exp: Expr> => VarAssign(var, exp);


// Operators

ExprOp: Operator = {
    AND => Operator::And,
    OR => Operator::Or
}

LogicOp: Operator = {
    LT => Operator::LessThan,
    GT => Operator::GreaterThan,
    NOTEQUAL => Operator::NotEq,
    EQUAL => Operator::Eq,
}

MathOp: Operator = {
    ADD => Operator::Add,
    SUB => Operator::Sub,
}

FactorOp: Operator = {
    MUL => Operator::Mul,
    DIV => Operator::Div,
}

PipeOp: Operator = PIPE => Operator::Pipe;

// Expressions

Tier<Operator, NextTerm>: Box<Expr> = {
    Tier<Operator, NextTerm> Operator NextTerm => Box::new(Expr::Op(<>)),
    NextTerm
}

pub Expr: Box<Expr> = Tier<ExprOp, LogicExpr>;

LogicExpr: Box<Expr> = {
    MathExpr LogicOp MathExpr => Box::new(Expr::Op(<>)),
    MathExpr,
}

MathExpr: Box<Expr> = Tier<MathOp, Term>;

Term: Box<Expr> = Tier<FactorOp, Factor>;

Factor: Box<Expr> = {
    <factor:Factor> PipeOp FORWARD <id:Boxed<Id>> => Box::new(Expr::Op(factor, Operator::ForwardPipe, id)),
    Factor PipeOp Boxed<Id> => Box::new(Expr::Op(<>)),
    Item,
}

Item: Box<Expr> = {
    VarReference => Box::new(Expr::Var(<>)),
    Const => Box::new(Expr::Const(<>)),
    LPAREN <Expr> RPAREN => Box::new(Expr::ParenthOp(<>)),
}


match {
    r#""[^"]*""# => STRING,
    "&&" => AND,
    "||" => OR,
    "<" => LT,
    ">" => GT,
    "!=" => NOTEQUAL,
    "==" => EQUAL,
    "+" => ADD,
    "-" => SUB,
    "*" => MUL,
    "/" => DIV,
    "|>" => PIPE,
    "forward" => FORWARD,
    "int" => T_INT,
    "float" => T_FLOAT,
    "bool" => T_BOOl,
    "str" => T_STR,
    "DataFrame" => T_DATAFRAME,
    "void" => T_VOID,
    "let" => LET,
    "true" => TRUE,
    "false" => FALSE,
    "(" => LPAREN,
    ")" => RPAREN,
    "[" => LBRACKET,
    "]" => RBRACKET,
    "][" => RLBRACKET,
    "=" => ASSIGN,
    "," => COMMA,
    ":" => COLON,
    r"[a-z_A-Z]+[0-9_a-zA-Z]*\(" => CALL_ID,
    r"[a-z_A-Z]+[0-9_a-zA-Z]*" => ID,
    r"[0-9]+\.[0-9]+" => FLOAT,
    r"[0-9]+" => NUMBER,
} else {
    _
}

