use std::str::FromStr;
use crate::syntax::{Expr, Operator, TypeConst, DataType, Index, Dimension, Variable, VarRef, FunctionParam, FunctionSignature};
use crate::syntax;

grammar;


// Macros

Comma<TItem>: Vec<TItem> = {
    <mut vec: (<TItem> COMMA)*> <c: TItem?> => match c {
        None => vec,
        Some(c) => {
            vec.push(c);
            vec
        }
    }
}

Boxed<T>: Box<T> = T => Box::new(<>);

// Types

Type: DataType = {
    T_INT => DataType::Int,
    T_FLOAT => DataType::Float,
    T_BOOl => DataType::Bool,
    T_STR => DataType::String,
    T_DATAFRAME => DataType::DataFrame,
}

FunctionType: DataType = {
    Type,
    T_VOID => DataType::Void
}

Int: TypeConst = NUMBER => TypeConst::Int(i32::from_str(<>).unwrap());

Float: TypeConst = <f: FLOAT> => TypeConst::Float(f.parse::<f32>().unwrap());

Bool: TypeConst = {
    TRUE => TypeConst::Bool(true),
    FALSE => TypeConst::Bool(false)
}

Str: TypeConst = {
    <s: STRING> => TypeConst::String(String::from(&s[1..s.len() - 1]))
}

VectorConst: TypeConst = LBRACKET <Comma<Expr>> RBRACKET => TypeConst::Vector(<>);

SimpleConst = {
    Int,
    Str,
    Float,
    Bool
}

Const = {
    SimpleConst,
    VectorConst
}

Id: Expr = ID => Expr::Var(VarRef {
    id: String::from(<>),
    indexing: None
});

// Variables

Indexing: Index = {
    <begin:Expr> COLON <end:Expr> => Index::Range(begin, end),
    <idx:Expr> => Index::Simple(idx)
}

Dimension: Dimension = {
    LBRACKET <a: Expr> RLBRACKET <b: Expr> RBRACKET => Dimension(2, vec![a, b]),
    LBRACKET <Expr> RBRACKET => Dimension(1, vec![<>])
}

VarDeclaration: Variable = {
    LET <id: ID> COLON <dtype: Type> <dim: Dimension?> <value: (ASSIGN <Expr>)?> SEMICOLON => Variable {
        id: String::from(id),
        data_type: dtype,
        dimension: dim.unwrap_or(Dimension(0, vec![])),
        value: value
    }
}

// TODO Fix w custom tokenizer, allow multiple dimensions and raise error on higher than two
VarRefIndex: Vec<Index> = {
    LBRACKET <Indexing> RBRACKET  => vec![<>],
    LBRACKET <a:Indexing> RLBRACKET <b:Indexing> RBRACKET => vec![<>]
}

VarReference: VarRef = {
    <id: ID> <indexing: VarRefIndex?> => VarRef {
        id: String::from(id),
        indexing: indexing
    } 
}

VarAssignment: syntax::Statement = <var: VarReference> ASSIGN <exp: Expr> => syntax::Statement::VarAssign(var, exp);


// Operators

ExprOp: Operator = {
    AND => Operator::And,
    OR => Operator::Or
}

LogicOp: Operator = {
    LT => Operator::LessThan,
    GT => Operator::GreaterThan,
    NOTEQUAL => Operator::NotEq,
    EQUAL => Operator::Eq,
}

MathOp: Operator = {
    ADD => Operator::Add,
    SUB => Operator::Sub,
}

FactorOp: Operator = {
    MUL => Operator::Mul,
    DIV => Operator::Div,
}

PipeOp: Operator = PIPE => Operator::Pipe;

// Expressions

Tier<Operator, NextTerm>: Box<Expr> = {
    Tier<Operator, NextTerm> Operator NextTerm => Box::new(Expr::Op(<>)),
    NextTerm
}

Expr: Box<Expr> = Tier<ExprOp, LogicExpr>;

LogicExpr: Box<Expr> = {
    MathExpr LogicOp MathExpr => Box::new(Expr::Op(<>)),
    MathExpr,
}

MathExpr: Box<Expr> = Tier<MathOp, Term>;

Term: Box<Expr> = Tier<FactorOp, Factor>;

Factor: Box<Expr> = {
    <factor:Factor> PipeOp FORWARD <id:Boxed<Id>> => Box::new(Expr::Op(factor, Operator::ForwardPipe, id)),
    Factor PipeOp Boxed<Id> => Box::new(Expr::Op(<>)),
    Item,
}

Item: Box<Expr> = {
    VarReference => Box::new(Expr::Var(<>)),
    Const => Box::new(Expr::Const(<>)),
    LPAREN <Expr> RPAREN => Box::new(Expr::ParenthOp(<>)),
    FunctionCall => Box::new(<>)
}

// Functions


FuncParam: FunctionParam = <id: ID> COLON <dtype: Type> => FunctionParam(String::from(id), dtype);

CallParams = Comma<Expr>;

FunctionParams = Comma<FuncParam>;

FnSignature: FunctionSignature = FN <id: CALL_ID> <params: FunctionParams> RPAREN COLON <dtype: Type> => FunctionSignature {
    id:  String::from(&id[..id.len() - 1]),
    data_type: dtype,
    params: params
};

FunctionItem: syntax::Statement = {
    Statement,
    ReturnStatement,
    VarDeclaration => syntax::Statement::VarDeclaration(<>),
}

FunctionBlock: syntax::Block = LCBRACKET <statements: FunctionItem*> RCBRACKET => syntax::Block(statements);

Function: syntax::Function = <signature: FnSignature> <block: FunctionBlock> => syntax::Function {
    signature: signature,
    block: block
};

FunctionCall: Expr = <id: CALL_ID> <params: CallParams> RPAREN => 
    Expr::FunctionCall(String::from(&id[..id.len() - 1]), params);

ReturnStatement: syntax::Statement = RETURN <Expr> SEMICOLON => syntax::Statement::Return(<>);

Statement = {
    Condition,
    Loop,
    LineStatement
}

GlobalStatement: syntax::Statement = {
    Function => syntax::Statement::FunctionDeclaration(<>),
    Statement,
    VarDeclaration =>  syntax::Statement::VarDeclaration(<>)
}

Block: syntax::Block = {
    Statement => syntax::Block(vec![<>]),
    StrictBlock
}

LineStatement: syntax::Statement = {
    <VarAssignment> SEMICOLON,
    <Expr> SEMICOLON => syntax::Statement::Expression(<>)
}

ElseBlock: syntax::Block = {
    Condition => syntax::Block(vec![<>]),
    StrictBlock
}

Condition: syntax::Statement = {
    IF <condition: Expr> RPAREN <if_block: StrictBlock> <else_block: (ELSE <ElseBlock>)?> => 
        syntax::Statement::If {
            condition: condition,
            if_block: if_block,
            else_block: else_block
        }
}

StrictBlock: syntax::Block = LCBRACKET <statements: Statement*> RCBRACKET => syntax::Block(statements);

Loop = {
    ForLoop,
    WhileLoop
}

ForLoop: syntax::Statement = FOR <id: ID> IN <iterable: Expr> RPAREN <block: Block> =>
    syntax::Statement::For {
        iterator_id: String::from(id),
        iterable: iterable,
        block: block
    };

WhileLoop: syntax::Statement = WHILE <condition: Expr> RPAREN <block: Block> =>
    syntax::Statement::While {
        condition: condition,
        block: block
    };

pub Program: syntax::Program = GlobalStatement* => syntax::Program(<>);

match {
    r#""[^"]*""# => STRING,
    "&&" => AND,
    "||" => OR,
    "<" => LT,
    ">" => GT,
    "!=" => NOTEQUAL,
    "==" => EQUAL,
    "+" => ADD,
    "-" => SUB,
    "*" => MUL,
    "/" => DIV,
    "|>" => PIPE,
    "forward" => FORWARD,
    "int" => T_INT,
    "float" => T_FLOAT,
    "bool" => T_BOOl,
    "str" => T_STR,
    "DataFrame" => T_DATAFRAME,
    "void" => T_VOID,
    "let" => LET,
    "true" => TRUE,
    "false" => FALSE,
    "fn" => FN,
    "return" => RETURN,
    "for",
    r"for\s*\(" => FOR, // TODO Fix joint ( w/ custom tokenizer,
    "while",
    r"while\s*\(" => WHILE,
    "in" => IN,
    "(" => LPAREN,
    ")" => RPAREN,
    "if",
    r"if\s*\(" => IF,
    "else" => ELSE,
    "{" => LCBRACKET,
    "}" => RCBRACKET,
    "[" => LBRACKET,
    "]" => RBRACKET,
    "][" => RLBRACKET,
    "=" => ASSIGN,
    "," => COMMA,
    ":" => COLON,
    ";" => SEMICOLON,
    r"[a-z_A-Z]+[0-9_a-zA-Z]*" => ID,
    r"[0-9]+\.[0-9]+" => FLOAT,
    r"[0-9]+" => NUMBER,
} else {
    r"[a-z_A-Z]+[0-9_a-zA-Z]*\(" => CALL_ID,
    _
}

